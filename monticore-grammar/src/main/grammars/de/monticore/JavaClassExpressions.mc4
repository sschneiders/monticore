/* (c) https://github.com/MontiCore/monticore */

package de.monticore;

component grammar JavaClassExpressions extends de.monticore.CommonExpressions{
	
external ELiteral;

external EType;

external EReturnType;

external ETypeArguments;

SuperExpression implements Expression <270> =
	Expression "." "super" SuperSuffix;
	
GenericInvocationExpression implements Expression <260> =
	Expression "." PrimaryGenericInvocationExpression;
	
TypeCastExpression implements Expression <230> =
	"(" EType ")" Expression;
        
PrimarySuperExpression implements Expression <330>
	= "super";
	
LiteralExpression implements Expression <340>
    = ELiteral;
        
ClassExpression implements Expression <360>
    = EReturnType "." "class";
   
PrimaryGenericInvocationExpression implements Expression <370>
    = ETypeArguments GenericInvocationSuffix
    ;

GenericInvocationSuffix
    =   ["super"] SuperSuffix
    |   ["this"] Arguments
    |   Name Arguments
    ;
    
SuperSuffix
    =   Arguments
    |   "." ETypeArguments? Name Arguments?
    ;
    
NameExpression implements Expression <350>
    = Name;
    
InstanceofExpression implements Expression <140> 
	= Expression "instanceof" EType;
}
